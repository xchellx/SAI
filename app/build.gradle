apply plugin: 'com.android.application'

android {
    compileSdkVersion 35
    defaultConfig {
        applicationId "com.aefyr.sai"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 29
        versionCode 60
        versionName "4.5"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
            buildConfigField "int", "DEFAULT_THEME", "0"
            buildConfigField "int", "DEFAULT_DARK_THEME", "1"
            buildConfigField "boolean", "HIDE_DONATE_BUTTON", "false"
        }

        fdroid {
            dimension "version"
            applicationId "com.aefyr.sai.fdroid"
            buildConfigField "int", "DEFAULT_THEME", "6"
            buildConfigField "int", "DEFAULT_DARK_THEME", "11"
            buildConfigField "boolean", "HIDE_DONATE_BUTTON", "false"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    lint {
        disable.add("ExpiredTargetSdkVersion")
    }

    namespace 'com.aefyr.sai'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-service:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-testing:2.2.0'
    implementation 'androidx.arch.core:core-testing:2.2.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.tomergoldst.android:tooltips:1.0.11'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    implementation 'com.github.aefyr:flexfilter:0.9-alpha'
    implementation 'com.github.aefyr:pseudoapksigner:1.6'

    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"

    //Google services
    normalImplementation 'com.google.firebase:firebase-analytics:22.1.2'
    normalImplementation 'com.google.firebase:firebase-crashlytics:19.2.1'
    normalImplementation 'com.android.billingclient:billing:7.1.1'

    //Shizuku/Sui
    implementation "dev.rikka.shizuku:api:11.0.3"
    implementation "dev.rikka.shizuku:provider:11.0.3"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Normal")){
    println "Google Play build detected, applying Google Services and Crashlytics plugins"
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}


